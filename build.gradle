buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "io.ratpack:ratpack-gradle:1.0.0"
        classpath 'de.undercouch:gradle-download-task:2.0.0'
    }
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: 'de.undercouch.download'

repositories {
    jcenter()
}

dependencies {
    compile ratpack.dependency('session')
    compile 'joda-time:joda-time:2.8.2'
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile 'oauth.signpost:signpost-commonshttp4:1.2.1.2'
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7'
    runtime 'org.slf4j:slf4j-simple:1.7.10'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testRuntime 'cglib:cglib:3.1'
}

task javaScriptDependencies << {
    def javaScriptLibDirectory = 'src/ratpack/public/js/lib'
    new File(javaScriptLibDirectory).mkdirs()
    fileTree(dir: javaScriptLibDirectory).each{ delete it }
    download {
        src 'http://momentjs.com/downloads/moment.js'
        dest javaScriptLibDirectory
    }
    download {
        src 'https://github.com/mbostock/d3/releases/download/v3.5.6/d3.zip'
        dest javaScriptLibDirectory
    }
    copy {
        from zipTree(file("$javaScriptLibDirectory/d3.zip"))
        into javaScriptLibDirectory
    }
    download {
        src 'http://builds.handlebarsjs.com.s3.amazonaws.com/handlebars-v4.0.2.js'
        dest "$javaScriptLibDirectory/handlebars.js"
    }
}

[run, installDist]*.dependsOn javaScriptDependencies

applicationDistribution.from('config.groovy') {
    into 'app'
}

task stage {
      dependsOn installDist
}
